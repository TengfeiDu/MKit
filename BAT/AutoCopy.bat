::定期备份文件到指定位置
@echo off
::::::::::::::::::::::::::::::::::::::::::::::::::::::

::脚本安装路径
set CPP=c:\AutoCopy
set configFile=%CPP%\config.txt

::H:\共享,J:\定时备份\共享
::G:\规划部初步,J:\定时备份\规划部初步
::G:\规划部定型,J:\定时备份\规划部定型
::G:\规划部临时共享,J:\定时备份\规划部临时共享
::F:\研发部电控,J:\定时备份\研发部电控
::F:\研发部机械,J:\定时备份\研发部机械
::F:\研发部临时共享,J:\定时备份\研发部临时共享
::F:\研发部软件,J:\定时备份\研发部软件
::G:\技术部电控,J:\定时备份\技术部电控
::E:\技术部机械,J:\定时备份\技术部机械
::G:\技术部结构,J:\定时备份\技术部结构
::E:\技术部临时共享,J:\定时备份\技术部临时共享
::M:\技术中心临时共享,J:\定时备份\技术中心临时共享
::M:\交接文件,J:\定时备份\交接文件
::M:\下单文件,J:\定时备份\下单文件
::M:\原始方案,J:\定时备份\原始方案

::准备日志文件路径
call:getTime shijian

set logDir=log
if not exist %CPP%\%logDir% md %CPP%\%logDir%
::"xxx\log\xxxx-xx-xx.log"
set logFile="%CPP%\%logDir%\%shijian:~0,10%.log"
::今天的日期"xxxx-xx-xx"
set nowDate=%shijian:~0,10%

::::::::::::::::::::::::::::::::::::::::::::::::::::::
::       		程序入口                    ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::
::每3天拷贝...
::set /a flag2=%nowDate:~8,2% %% 3
::::::::::::::::::::::::::::::::::::::::::::::::::::::
for /f "delims=, tokens=1-2" %%i in (%configFile%) do (
	if not exist %%j md %%j
	call:log "开始拷贝[%%i]---[%%j]..."
	xcopy /E /C /H /Y "%%i" "%%j"
	call:log "拷贝完成[%%i]---[%%j]..."
	cscript "%CPP%\sleep.vbs" 3000 >nul
)
::::::::::::::::::::::::::::::::::::::::::::::::::::::
call:log "任务结束..."
exit 0
::::::::::::::::::::::::::::::::::::::::::::::::::::::


::::::::::::::::::::::::::::::::::::::::::::::::::::::
::                   自定义函数                     ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::
:getTime
	set nian=%date:~0,4%
	set yue=%date:~5,2%
	set ri=%date:~8,2%
	set "%~1=%nian%-%yue%-%ri% %time%"
goto:eof
::::::::::::::::::::::::::::::::::::::::::::::::::::::
:log
	call:getTime nowTime
	echo %nowTime% %~1 >> "%logFile%"
goto:eof
::::::::::::::::::::::::::::::::::::::::::::::::::::::
